
// Начало   Чесноков М.С. 2020-10-30 F1-23
//
// Параметры:
//  ИмяФормы - строка - имя открываемой формы
// 
// Возвращаемое значение:
//   - строка, полное имя формы
//
Функция ПолучитьПолноеИмяФормы(ИмяФормы,ЭтаФорма) Экспорт
 
    СимволТчк = ".";
	
	ПозицияТочки = СтрДлина(ЭтаФорма.ИмяФормы);
	
	Пока НЕ Сред(ЭтаФорма.ИмяФормы, ПозицияТочки, 1) = СимволТчк Цикл
		
		ПозицияТочки = ПозицияТочки - 1;
		
	КонецЦикла; 
	
    Возврат Лев(ЭтаФорма.ИмяФормы, ПозицияТочки) + ИмяФормы;
 
КонецФункции
// Конец    Чесноков М.С. 2020-10-30 F1-23

// Процедура - Выводит табличный документ в типовой форме БСП.
//
// Параметры:
//  ТабличныйДокумент     - табличный документ - документ для вывода на печать.
//  ЗаголовокОтчета       - строка - Заголовок отчета.
//  Форма                 - форма - Текущая форма.
//
Процедура ПечатьТабличногоДокумента(ТабличныйДокумент,ЗаголовокОтчета,Форма) Экспорт
	
	Если ТабличныйДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;

	//Создаём новую коллекцию печатных форм
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ЗаголовокОтчета);
	
	//Добавляем в коллекцию сформированный табличный документ
	КоллекцияПечатныхФорм[0].ТабличныйДокумент = ТабличныйДокумент;
	
	//Устанавливаем параметры печати (при необходимости)
	КоллекцияПечатныхФорм[0].Экземпляров = 1;
	КоллекцияПечатныхФорм[0].СинонимМакета = ЗаголовокОтчета;  //Так будет выглядеть имя файла при сохранении в файл из формы "Печать документов"
	
	//Вывод через стандартную процедуру БСП
 	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, Неопределено, Форма);
	
КонецПроцедуры

// Устанавливает значения по-умолчанию в отчетах для собственников.
//
//	Параметры:
//		Отчет - объект, отчет объект.
//		Параметры - структура, параметры в отчете, которые нужно установить.
Процедура УстановитьЗначениеПараметровДляСобственниковПоУмолчанию(Отчет) Экспорт
	
	ПараметрыСтруктура = фуОтчетыСерверФинансовыйУчет.ПолучитьПараметрыОтчетовДляСобсвтенников();
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;
	
	Если ПараметрыСтруктура.Свойство("ПериодВОтчете") Тогда
	
		ПараметрКомпоновки = Новый ПараметрКомпоновкиДанных("ПериодВОтчете");
		ПараметрСКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновки);
		ПараметрСКД.Значение = ПараметрыСтруктура.ПериодВОтчете;
		ПараметрСКД.Использование = Истина;
		
	КонецЕсли;
	                           	
	Если ЗначениеЗаполнено(ПараметрСКД.ИдентификаторПользовательскойНастройки) Тогда
	
		ПользовательскийПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
		   ПараметрСКД.ИдентификаторПользовательскойНастройки);
		Если ТипЗнч(ПользовательскийПараметр) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ПользовательскийПараметр.Значение = ПараметрСКД.Значение;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры


