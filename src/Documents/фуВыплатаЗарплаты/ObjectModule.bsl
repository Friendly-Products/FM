#Область о // Стандартные процедуры и функции:

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
    
    ЗаполнитьРеквизитыОбъекта();
    
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
    
КонецПроцедуры

Процедура ПроверкаВозможностиПроведения(Отказ, Режим)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) Тогда
		Если Не ЭтотОбъект.ДокументОснование.Проведен Тогда
			Отказ = Истина;
			фуОбщийКлиентСервер.СообщитьПользователю("Внимание! Документ основание: "+ЭтотОбъект.ДокументОснование+" не проведен.");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	ПроверкаВозможностиПроведения(Отказ, Режим);
    
    УстановитьПривилегированныйРежим(Истина);
    
	ЗаписьДвижений_фуОстаткиНаРасчетныхСчетах(Отказ,Режим);
	ЗаписьДвижений_фуДвиженияДенежныхСредств(Отказ,Режим);
	ЗаписьДвижений_фуНачисленияУдержанияПоСотрудникам(Отказ,Режим);
	ЗаписьДвижений_фуДоходыИРасходы(Отказ,Режим);
    
    // +++ Чесноков М.С. 2021-05-20 F1-84
    ЗаписьДвижений_фуЗатратыПоВыплатеЗарплаты(Отказ,Режим);
    // --- Чесноков М.С. 2021-05-20 F1-84
    
    УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.фуНачислениеЗарплаты") Тогда
	
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения.Ссылка; 
		ЭтотОбъект.ПроцентКомиссии = ПолучитьПроцентКомиссииПоУмолчанию();
		ЭтотОбъект.Выплата.Очистить();
	
		Для каждого стрНачисления из ДанныеЗаполнения.Начисления Цикл
			
			стрВыплата = ЭтотОбъект.Выплата.Добавить();
			ЗаполнитьЗначенияСвойств(стрВыплата,стрНачисления);
			стрВыплата.Комиссия = стрВыплата.Сумма * ЭтотОбъект.ПроцентКомиссии / 100;
			стрВыплата.ВидНачисленияУдержания = стрНачисления.Начисление;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПроцентКомиссииПоУмолчанию() Экспорт
	
	Возврат 2.5;
	
КонецФункции

#КонецОбласти

#Область о // Заполнение реквизитов объекта:

Процедура ЗаполнитьРеквизитыОбъекта()
	
	ЗаполнениеДатыВыплаты();
	ЗаполнениеПроцентаКомиссии();
    
    // +++ Чесноков М.С. 2021-04-09 F1-66
    ЗаполнениеСтатейЗатратПоУмолчанию();
	// --- Чесноков М.С. 2021-04-09 F1-66
    
    // +++ Чесноков М.С. 2021-05-19 F1-83
    ЗаполнениеСуммЗатрат();
    // --- Чесноков М.С. 2021-05-19 F1-83 

КонецПроцедуры

// +++ Чесноков М.С. 2021-05-19 F1-83
Процедура ЗаполнениеСуммЗатрат()
    
    // Затраты везде учитываются построчно, так как сладывать и округлять не тоже, что 
    //округлять, а потом складывать:    

	ОсновныеВалюты	= фуОперацииСВалютамиКлиентСервер.ПолучитьОсновныеВалютыСервер();
	ОсновнаяВалюта  = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
    
    СуммаЗатраты    = 0;
    СуммаКомиссия   = 0;
    Для каждого ВыплатаСтрока Из ЭтотОбъект.Выплата Цикл
        
        Если ОсновнаяВалюта <> ОсновныеВалюты.Доллар Тогда
            СуммаЗатраты     = СуммаЗатраты + РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыплатаСтрока.Сумма, ОсновнаяВалюта, ОсновныеВалюты.Доллар, ЭтотОбъект.ДатаВыплаты);
            СуммаКомиссия    = СуммаКомиссия + РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыплатаСтрока.Комиссия, ОсновнаяВалюта, ОсновныеВалюты.Доллар, ЭтотОбъект.ДатаВыплаты);
        Иначе
            СуммаЗатраты     = СуммаЗатраты + ВыплатаСтрока.Сумма; 
            СуммаКомиссия    = СуммаКомиссия + ВыплатаСтрока.Комиссия;
        КонецЕсли;
    	
    КонецЦикла;    
    
    ЭтотОбъект.СуммаДолларыЗатраты              = СуммаЗатраты;
    ЭтотОбъект.СуммаДолларыЗатратыНаКомиссию    = СуммаКомиссия;
    
КонецПроцедуры
// --- Чесноков М.С. 2021-05-19 F1-83 

// +++ Чесноков М.С. 2021-04-09 F1-66
Процедура ЗаполнениеСтатейЗатратПоУмолчанию()
    
    Если Не ЗначениеЗаполнено(ЭтотОбъект.СтатьяЗатрат) Тогда
        ОплатаТрудаПерсоналаСтатьяЗатрат = Справочники.фуСтатьиЗатрат.ОплатаТрудаПерсонала;
        ЭтотОбъект.СтатьяЗатрат = ОплатаТрудаПерсоналаСтатьяЗатрат;                   
    КонецЕсли;  
    Если Не ЗначениеЗаполнено(ЭтотОбъект.СтатьяЗатратНаКомиссию) Тогда
        КомиссииПриВыплатеЗарплатыСтатьяЗатрат = Справочники.фуСтатьиЗатрат.КомиссииПриВыплатеЗарплаты;
        ЭтотОбъект.СтатьяЗатратНаКомиссию = КомиссииПриВыплатеЗарплатыСтатьяЗатрат;    
    КонецЕсли;  
    
КонецПроцедуры
// --- Чесноков М.С. 2021-04-09 F1-66

Процедура ЗаполнениеДатыВыплаты()
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ДатаВыплаты) Тогда
		ЭтотОбъект.ДатаВыплаты = ЭтотОбъект.Дата;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнениеПроцентаКомиссии()
	
    Если ЭтотОбъект.ПроцентКомиссии = 0 Тогда
        Если ЭтотОбъект.ЭтоНовый() Тогда
            ЭтотОбъект.ПроцентКомиссии = ПолучитьПроцентКомиссииПоУмолчанию();	
        КонецЕсли;		
	КонецЕсли;

	Если Не ЭтотОбъект.КорректировкаКомиссии Тогда
		Для каждого ВыплатаСтрока из ЭтотОбъект.Выплата Цикл
			Если Не ЗначениеЗаполнено(ВыплатаСтрока.Комиссия) Тогда
				ВыплатаСтрока.Комиссия = ВыплатаСтрока.Сумма * ЭтотОбъект.ПроцентКомиссии / 100;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область о // Запись движений фмОстаткиНаРасчетныхСчетах:

Процедура ЗаписьДвижений_фуОстаткиНаРасчетныхСчетах(Отказ,Режим)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.фуОстаткиНаРасчетныхСчетах.Записывать = Истина;
	
	ОсновнаяВалюта   = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
	ИтоговаяСумма 	 = ЭтотОбъект.Выплата.Итог("Сумма");
	
	Если ИтоговаяСумма <> 0 Тогда
		
		Движение = Движения.фуОстаткиНаРасчетныхСчетах.ДобавитьРасход();
		Движение.Период = ЭтотОбъект.Дата;
		Движение.РасчетныйСчет = ЭтотОбъект.РасчетныйСчет;
		Движение.Валюта = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
		Движение.Сумма = ИтоговаяСумма;
		Движение.Основание = "Выплата зарплаты";
		
	КонецЕсли;
	
	ИтоговаяСумма 	 = ЭтотОбъект.Выплата.Итог("Комиссия");
	Если ИтоговаяСумма <> 0 Тогда
		
		Движение = Движения.фуОстаткиНаРасчетныхСчетах.ДобавитьРасход();
		Движение.Период = ЭтотОбъект.Дата;
		Движение.РасчетныйСчет = ЭтотОбъект.РасчетныйСчет;
		Движение.Валюта = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
		Движение.Сумма = ИтоговаяСумма;
		Движение.Основание = "Комиссия";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область о // Запись движений фмНачисленияУдержанияПоСотрудникам:

Функция ПолучитьОстаткиПоСотрудникам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	фуВыплатаЗарплатыВыплата.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ втСотрудники
	|ИЗ
	|	Документ.фуВыплатаЗарплаты.Выплата КАК фуВыплатаЗарплатыВыплата
	|ГДЕ
	|	фуВыплатаЗарплатыВыплата.Ссылка = &фмВыплатаЗарплатыСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	фмНачисленияУдержанияПоСотрудникамОстатки.Сотрудник КАК Сотрудник,
	|	фмНачисленияУдержанияПоСотрудникамОстатки.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	фмНачисленияУдержанияПоСотрудникамОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА фмНачисленияУдержанияПоСотрудникамОстатки.Валюта = &ОсновнаяВалюта
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриоритетСписания,
	|	фмНачисленияУдержанияПоСотрудникамОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.фуНачисленияУдержанияПоСотрудникам.Остатки(
	|			&Дата1,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					втСотрудники.Сотрудник КАК Сотрудник
	|				ИЗ
	|					втСотрудники КАК втСотрудники)) КАК фмНачисленияУдержанияПоСотрудникамОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриоритетСписания УБЫВ";
	
	
	Запрос.УстановитьПараметр("Дата1",ЭтотОбъект.ДатаВыплаты);
	Запрос.УстановитьПараметр("ОсновнаяВалюта",ЭтотОбъект.РасчетныйСчет.ВалютаСчета);
	Запрос.УстановитьПараметр("фуВыплатаЗарплатыСсылка",ЭтотОбъект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаписьДвижений_фуНачисленияУдержанияПоСотрудникам(Отказ,Режим);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.фуНачисленияУдержанияПоСотрудникам.Записывать = Истина;
    ВалютаСчета = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
    Для каждого стрВыплата из Выплата Цикл
        
        Если стрВыплата.Сумма <> 0 Тогда
    		Движение = Движения.фуНачисленияУдержанияПоСотрудникам.ДобавитьРасход();
    		ЗаполнитьЗначенияСвойств(Движение,стрВыплата);
    		Движение.Период = НачалоМесяца(ЭтотОбъект.ДатаВыплаты);
            Движение.Валюта = ВалютаСчета;
            Движение.ВидЗарплаты = ЭтотОбъект.ВидЗарплаты;
        КонецЕсли;    
        
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область о // Запись движений фмДвиженияДенежныхСредств:

Процедура ЗаписьДвижений_фуДвиженияДенежныхСредств(Отказ,Режим)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.фуДвиженияДенежныхСредств.Записывать = Истина;
	
	ОсновныеВалюты			= фуОперацииСВалютамиКлиентСервер.ПолучитьОсновныеВалютыСервер();
	ОсновнаяВалюта          = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
    
    Если ЭтотОбъект.СуммаДолларыЗатраты <> 0 Тогда
        
        Движение = Движения.фуДвиженияДенежныхСредств.Добавить();
        Движение.Период = ЭтотОбъект.Дата;
        Движение.СтатьяДДС = ЭтотОбъект.СтатьяЗатрат;
        Движение.Сумма = (-1) * ЭтотОбъект.СуммаДолларыЗатраты;
        Движение.Основание = "Затраты на заработную плату.";
        
    КонецЕсли;
    
    Если ЭтотОбъект.СуммаДолларыЗатратыНаКомиссию <> 0 Тогда 
    	
    	Движение = Движения.фуДвиженияДенежныхСредств.Добавить();
    	Движение.Период = ЭтотОбъект.Дата;
    	Движение.СтатьяДДС = ЭтотОбъект.СтатьяЗатратНаКомиссию;
    	Движение.Сумма = (-1) * ЭтотОбъект.СуммаДолларыЗатратыНаКомиссию;
        Движение.Основание = "Затраты на комиссию.";

    КонецЕсли;
    // --- Чесноков М.С. 2021-05-19 F1-83
		
КонецПроцедуры

#КонецОбласти

#Область о // Запись движений фмДоходыИРасходы:

Процедура ЗаписьДвижений_фуДоходыИРасходы(Отказ,Режим)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.фуДоходыИРасходы.Записывать = Истина;
	
	ОсновныеВалюты	= фуОперацииСВалютамиКлиентСервер.ПолучитьОсновныеВалютыСервер();
	ОсновнаяВалюта	= ЭтотОбъект.РасчетныйСчет.ВалютаСчета;

	// Сумма комиссии:
    // +++ Чесноков М.С. 2021-04-12 F1-66
	СтатьяКомиссия 			= ЭтотОбъект.СтатьяЗатратНаКомиссию;
    // --- Чесноков М.С. 2021-04-12 F1-66 
	
	Для каждого ВыплатаСтрока из ЭтотОбъект.Выплата Цикл
		
		Если ВыплатаСтрока.Комиссия <> 0 Тогда
			
			ИтоговаяСумма = ВыплатаСтрока.Комиссия; 
			Если ОсновнаяВалюта <> ОсновныеВалюты.Доллар Тогда
				ИтоговаяСумма = РаботаСКурсамиВалют.ПересчитатьВВалюту(ИтоговаяСумма, ОсновнаяВалюта, ОсновныеВалюты.Доллар, ЭтотОбъект.ДатаВыплаты);
			КонецЕсли;

			Движение = Движения.фуДоходыИРасходы.Добавить();
			Движение.Период = ЭтотОбъект.Дата;
			Движение.СтатьяЗатрат	= СтатьяКомиссия;
			Движение.Сумма   = (-1) * ИтоговаяСумма;
			Движение.Сотрудник      = ВыплатаСтрока.Сотрудник;
			Движение.Основание      = "Выплата зарплаты";
            // +++ Чесноков М.С. 2020-11-13 F1-19
            Движение.ВыплатаИлиНачислениеЗП = Истина;
            // --- Чесноков М.С. 2020-11-13 F1-19
                
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры   

#КонецОбласти

// +++ Чесноков М.С. 2021-05-20 F1-84
#Область о // Запись движений фмЗатратыПоВыплатеЗарплаты:

Процедура ЗаписьДвижений_фуЗатратыПоВыплатеЗарплаты(Отказ,Режим)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

    Движения.фуЗатратыПоВыплатеЗарплаты.Записывать = Истина;
    
    ОсновныеВалюты			= фуОперацииСВалютамиКлиентСервер.ПолучитьОсновныеВалютыСервер();
	ОсновнаяВалюта          = ЭтотОбъект.РасчетныйСчет.ВалютаСчета;
    
    Для каждого ВыплатаСтрока Из ЭтотОбъект.Выплата Цикл
        
        СуммаЗатратДоллары           = ВыплатаСтрока.Сумма;
        СуммаЗатратНаКомиссиюДоллары = ВыплатаСтрока.Комиссия;
        
        Если ОсновнаяВалюта <> ОсновныеВалюты.Доллар Тогда
            СуммаЗатратДоллары              = РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыплатаСтрока.Сумма, ОсновнаяВалюта, ОсновныеВалюты.Доллар, ЭтотОбъект.ДатаВыплаты);
            СуммаЗатратНаКомиссиюДоллары    = РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыплатаСтрока.Комиссия, ОсновнаяВалюта, ОсновныеВалюты.Доллар, ЭтотОбъект.ДатаВыплаты);
    	КонецЕсли;

        Если СуммаЗатратДоллары = 0 И СуммаЗатратНаКомиссиюДоллары = 0 Тогда
        	Продолжить;
        КонецЕсли; 
        
        Если СуммаЗатратДоллары <> 0 Тогда
            Движение = Движения.фуЗатратыПоВыплатеЗарплаты.Добавить();    
            Движение.Период         = ЭтотОбъект.Дата;
            Движение.РасчетныйСчет  = ЭтотОбъект.РасчетныйСчет;
            Движение.СтатьяЗатрат   = ЭтотОбъект.СтатьяЗатрат;
            Движение.ВалютаУправленческогоУчетаСумма   = (-1) * СуммаЗатратДоллары;
        КонецЕсли; 
        
        Если СуммаЗатратНаКомиссиюДоллары <> 0 Тогда
            Движение = Движения.фуЗатратыПоВыплатеЗарплаты.Добавить();    
            Движение.Период        = ЭтотОбъект.Дата;
            Движение.РасчетныйСчет = ЭтотОбъект.РасчетныйСчет;
            Движение.СтатьяЗатрат  = ЭтотОбъект.СтатьяЗатратНаКомиссию;
            Движение.ВалютаУправленческогоУчетаСумма  = (-1) * СуммаЗатратНаКомиссиюДоллары;
        КонецЕсли; 

    КонецЦикла; 

КонецПроцедуры   

#КонецОбласти
// --- Чесноков М.С. 2021-05-20 F1-84 
