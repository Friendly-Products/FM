#Область о // Стандартные процедуры и функции:

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ДатаПриемаНаРаботу) Тогда
		ЭтотОбъект.ДатаПриемаНаРаботу = ЭтотОбъект.Дата;
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ДатаПриказа) Тогда
		ЭтотОбъект.ДатаПриказа = ЭтотОбъект.ДатаПриемаНаРаботу;
	КонецЕсли; 
	
	// +++ Чесноков М.С. 2021-09-22 F1-121
	фуКадровыйУчетСервер.УдалениеСкановДокументаПередЗаписью(ЭтотОбъект);
	// --- Чесноков М.С. 2021-09-22 F1-121
	
	// +++ Чесноков М.С. 2021-10-26 F1-133
	Если Не ЗначениеЗаполнено(ЭтотОбъект.СтатусДокумента) Тогда
		Если НачалоГода(ЭтотОбъект.Дата) >= Дата("20210101000000") Тогда
			ЭтотОбъект.СтатусДокумента = Перечисления.фуСтатусыДокументов.КСогласованию;	
		КонецЕсли; 
	КонецЕсли; 
	// --- Чесноков М.С. 2021-10-26 F1-133 
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Сотрудник = Справочники.фуСотрудники.ПустаяСсылка();
	
	// +++ Чесноков М.С. 2021-09-22 F1-121
	ЭтотОбъект.СканыДокументовКадры.Очистить();
	// --- Чесноков М.С. 2021-09-22 F1-121
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Если ДанныеЗаполнения.Свойство("Сотрудник") Тогда
			ЭтотОбъект.Сотрудник = ДанныеЗаполнения.Сотрудник;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	ЗаписьДвижений_фуКадроваяИсторияСотрудников(Отказ,Режим);
    
	// +++ Чесноков М.С. 2021-11-10 F1-137
	// +++ Чесноков М.С. 2021-10-25 F1-133
	//фмКадровыйУчетСервер.ОтправитьОповещениеОДобавленииНовогоСотрудникаНаЭлектроннуюПочтуОтветсвеннымЛицам(ЭтотОбъект.Ссылка);
	// --- Чесноков М.С. 2021-10-25 F1-133 
	ТекстФоновойПроцедуры = "
	|//Тут передаем текст фоновой процедуры, это может быть вызов каких то процедур или полноценный код
	|//вызов процедур проще, т.к. тупо легче отлаживать, т.к. код в этой процедуре обычной отладке не поддастся,
	|//а при вызове процедуры или функции мы ставим там точку останова и включаем в отладке автоматическое
	|//подключение  фоновых заданий. Запускать можно любые методы для выполнения на сервере.
	|
	|фмКадровыйУчетСервер.ОтправитьОповещениеОДобавленииНовогоСотрудникаНаЭлектроннуюПочтуОтветсвеннымЛицам(ОбъектСсылка);";
	
	фуОбщийСервер.ЗапуститьФоновоеВыполнение(ТекстФоновойПроцедуры,Новый Структура("ОбъектСсылка",ЭтотОбъект.Ссылка));
	// --- Чесноков М.С. 2021-11-10 F1-137  
	 
КонецПроцедуры

#КонецОбласти

#Область о // Запись движений:

Процедура ЗаписьДвижений_фуКадроваяИсторияСотрудников(Отказ,Режим)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Сотрудник) Тогда
		Отказ = Истина;
		фуОбщийКлиентСервер.СообщитьПользователю("Внимание! Сотрудник не добавлен в справочник. Проведение отменено.");
		Возврат;
    КонецЕсли;
    
	Движение = Движения.фуКадроваяИсторияСотрудников.Добавить();
	ЗаполнитьЗначенияСвойств(Движение,ЭтотОбъект);
	Движение.Период = ЭтотОбъект.ДатаПриемаНаРаботу;
	Движение.ВидСобытия = Перечисления.фуВидыКадровыхСобытий.ПриемНаРаботу;
    
    Движения.фуКадроваяИсторияСотрудников.Записать(Истина);

КонецПроцедуры

#КонецОбласти
